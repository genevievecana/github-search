def Group(Closure closure) {
    closure.delegate = dependencies
    return closure
}

ext{
    libs = Group {
        implementation fileTree(dir: 'libs', include: ['*.jar'])
    }

    kotlin = Group {
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$versions.kotlin"
    }

    androidXUi = Group {
        implementation "androidx.appcompat:appcompat:$versions.appCompat"
        implementation "androidx.cardview:cardview:$versions.cardView"
        implementation "androidx.constraintlayout:constraintlayout:$versions.constraint"
        implementation "com.google.android.material:material:$versions.androidMaterial"
    }

    intuitAdjuster = Group {
        implementation "com.intuit.sdp:sdp-android:$versions.intuitAdjuster"
        implementation "com.intuit.ssp:ssp-android:$versions.intuitAdjuster"
    }

    glide = Group {
        implementation "com.github.bumptech.glide:glide:$versions.glide"
        kapt "com.github.bumptech.glide:compiler:$versions.glide"
    }

    retrofit = Group {
        implementation "com.squareup.retrofit2:retrofit:$versions.retrofit"
        implementation "com.squareup.retrofit2:adapter-rxjava2:$versions.retrofit"
        implementation "com.squareup.retrofit2:converter-moshi:$versions.retrofit"
        implementation "com.squareup.okhttp3:logging-interceptor:$versions.okHttp"
    }

    dagger = Group {
        implementation "com.google.dagger:dagger:$versions.dagger"
        kapt "com.google.dagger:dagger-compiler:$versions.dagger"
        kapt "com.google.dagger:dagger-android-processor:$versions.dagger"
        implementation "com.google.dagger:dagger-android:$versions.dagger"
    }

    moshi = Group {
        kapt "com.squareup.moshi:moshi-kotlin:$versions.moshi"
        implementation "com.squareup.moshi:moshi:$versions.moshi"
    }

    kotshi = Group {
        implementation "se.ansman.kotshi:api:$versions.kotshi"
        kapt "se.ansman.kotshi:compiler:$versions.kotshi"
    }

    lifecycle = Group {
        implementation "androidx.lifecycle:lifecycle-extensions:$versions.androidXArchi"
        kapt "androidx.lifecycle:lifecycle-compiler:$versions.androidXArchi"
        implementation "androidx.lifecycle:lifecycle-reactivestreams:$versions.androidXArchi"
    }

    rxjava = Group {
        implementation "io.reactivex.rxjava2:rxjava:$versions.rxjava"
        implementation "io.reactivex.rxjava2:rxandroid:$versions.rxandroid"
        implementation "io.reactivex.rxjava2:rxkotlin:$versions.rxkotlin"
    }

    test = Group {
        testImplementation "junit:junit:$versions.junit"
        testImplementation "org.mockito:mockito-core:$versions.mockito"
        testImplementation "androidx.arch.core:core-testing:$versions.androidXArchi"
        testImplementation "com.google.truth:truth:$versions.googleTruth"
        androidTestImplementation "androidx.test:runner:$versions.testRunner"
        androidTestImplementation "androidx.test:rules:$versions.testRunner"
        androidTestImplementation "androidx.test:core:$versions.androidXTest"
        androidTestImplementation "androidx.test.ext:junit:$versions.androidXTest"
        androidTestImplementation "androidx.test.espresso:espresso-core:$versions.espresso"
        androidTestImplementation "com.schibsted.spain:barista:$versions.barista"
    }

    appDependencies = Group {
        test()
        libs()
        kotlin()
        androidXUi()
        intuitAdjuster()
        glide()
        dagger()
        retrofit()
        moshi()
        kotshi()
        lifecycle()
        rxjava()
    }

}
